import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDirectory = project.layout.projectDirectory.dir("out")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly('be.maximvdw:MVdWPlaceholderAPI:3.0.1-SNAPSHOT') { transitive = false }
    compileOnly 'com.SirBlobman.combatlogx:CombatLogX-API:10.0.0.0-SNAPSHOT'
    implementation 'io.github.bananapuncher714:nbteditor:7.17.0', {
        exclude group: 'org.spigotmc'
    }
    implementation 'com.github.cryptomorin:XSeries:8.1.0'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.6.1-SNAPSHOT'

    compile name: 'Vault-1.6.7'
    compile name: 'CombatTagPlus'
    compile name: 'PvPManager-3.7.16'
    compile name: 'Factions-1.6.9.5-U0.1.14'
    compile name: 'MassiveCore'
    compile name: 'Factions'
    compile name: 'MyPet-2.3.4'
    compile name: 'BountyHunters-2.2.6'
    compile name: 'LeaderHeadsAPI'
    compile project(':duels-worldguard')
    compile project(':duels-worldguard-v6')
    compile project(':duels-worldguard-v7')
    compile project(':duels-api')
    compile 'com.google.code.gson:gson:2.8.7'

}

shadowJar {
    archiveFileName.set(parent.name + '-' + project.version + '.jar')
    dependencies {
        include(dependency(':duels-worldguard'))
        include(dependency(':duels-worldguard-v6'))
        include(dependency(':duels-worldguard-v7'))
        include(dependency(':duels-api'))
        include(dependency('com.google.code.gson:.*'))
        include(dependency('io.github.bananapuncher714:.*'))
    }
    def group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'io.github.bananapuncher714.nbteditor', group + 'nbteditor'
    relocate 'com.google.gson', group + 'gson'
    relocate "com.cryptomorin.xseries", group + 'xseries'
}

build.dependsOn(shadowJar)